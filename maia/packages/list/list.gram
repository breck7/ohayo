tooling onsave jtree build produceMaiaGrammar
listBasicNode
 catchAllCellType columnNameCell
 description Show 1 column as a text list.
 example List of world's telescopes
  samples.telescopes
   list.basic
 inScope labelNode
 string bodyStumpTemplate
  ol
   {items}
 string listItemStumpTemplate
  li
   span {label}
 javascript
  _getListItem(label) {
   return this.qFormat(this.listItemStumpTemplate, { label })
  }
  _getLabelColumnName() {
   // todo: more automatic! Need to fix our columns/keywords issues
   return this.getWord(1) || this.getSettingsStruct().label
  }
  getTileBodyStumpCode() {
   const labelColumnName = this._getLabelColumnName()
   const items = this.getRowsWithRowDisplayLimit()
    .map(row => this._getListItem(jtree.Utils.stripHtml(row.getRowOriginalValue(labelColumnName)), row))
    .join("\n")
   return this.qFormat(this.bodyStumpTemplate, { items })
  }
 string tileSize 400 400
 string dummyDataSetName telescopes
 string columnPredictionHints
  label getTitlePotential
 extends abstractChartNode
 crux list.basic
listLinksNode
 description Show 1 column as a list of links, using 1 column for the url.
 catchAllCellType columnNameCell
 example List of world's telescopes with links
  samples.telescopes
   list.links
 inScope labelNode linkNode
 string dummyDataSetName telescopes
 string listItemHakonTemplate
  li
   a {label}
    href {link}
 javascript
  _getUrlColumnName() {
   // todo: more automatic! Need to fix our columns/keywords issues
   return this.getWord(2) || this.getSettingsStruct().link
  }
  _getListItem(label, row) {
   const urlColumnName = this._getUrlColumnName()
   if (!urlColumnName) return super._getListItem(label, row)
   return this.qFormat(this.listItemHakonTemplate, { label, link: jtree.Utils.stripHtml(row.getRowOriginalValue(urlColumnName)) })
  }
 string columnPredictionHints
  label getTitlePotential
  link isLink
 extends listBasicNode
 crux list.links